#version 460 core
//===============================================================================================
// INPUT/OUTPUT
//===============================================================================================

// 4 threads are used for every dimension
layout (local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

// Output 3D texture
layout (rgba8, binding = 0) uniform image3D perlinWorleyTex;

//===============================================================================================
// CONSTANTS
//===============================================================================================

// Hash
#define UI0 1597334673U
#define UI1 3812015801U
#define UI2 uvec2(UI0, UI1)
#define UI3 uvec3(UI0, UI1, 2798796415U)
#define UIF (1.0 / float(0xffffffffU))

// 3D texture
const float resolution = 128.0f;

// Perlin
const int perlinID = 0;
const float perlinAmplitude = 1.0f;
const float perlinFrequency = 4.0f;
const int perlinOctaves = 8;
const float perlinLacunarity = 2.0f;
const float perlinGain = exp2(-0.85f);

// Worley
const int worleyID = 1;
const float worleyAmplitude = 0.8f;
const float worleyFrequency = 4.0f;
const int worleyOctaves = 3;
const float worleyLacunarity = 4.0f;
const float worleyGain = 0.2f;

//===============================================================================================
// STRUCTS
//===============================================================================================

struct fbm {
    float amplitude;
    float frequency;
    int octaves;
    float lacunarity;
    float gain;
};

//===============================================================================================
// METHODS
//===============================================================================================

// Hash function
vec3 hash33(vec3 p)
{
	uvec3 q = uvec3(ivec3(p)) * UI3;
	q = (q.x ^ q.y ^ q.z) * UI3;
	return -1.0 + 2.0 * vec3(q) * UIF;
}

// Converts/Remaps a value from one range to another, where x is value to be remapped,
// original range is [Lo, Ho] and a new one is [Ln, Hn].
float remap(float x, float Lo, float Ho, float Ln, float Hn)
{
    return (((x - Lo) / (Ho - Lo)) * (Hn - Ln)) + Ln;
}

// Calculates Perlin noise
float perlinNoise(vec3 x, float freq)
{
    // grid
    vec3 p = floor(x);
    vec3 w = fract(x);
    
    // quintic interpolant
    vec3 u = w * w * w * (w * (w * 6.0 - 15.0) + 10.0);
    
    // gradients
    vec3 ga = hash33(mod(p + vec3(0.0, 0.0, 0.0), freq));
    vec3 gb = hash33(mod(p + vec3(1.0, 0.0, 0.0), freq));
    vec3 gc = hash33(mod(p + vec3(0.0, 1.0, 0.0), freq));
    vec3 gd = hash33(mod(p + vec3(1.0, 1.0, 0.0), freq));
    vec3 ge = hash33(mod(p + vec3(0.0, 0.0, 1.0), freq));
    vec3 gf = hash33(mod(p + vec3(1.0, 0.0, 1.0), freq));
    vec3 gg = hash33(mod(p + vec3(0.0, 1.0, 1.0), freq));
    vec3 gh = hash33(mod(p + vec3(1.0, 1.0, 1.0), freq));
    
    // projections
    float va = dot(ga, w - vec3(0.0, 0.0, 0.0));
    float vb = dot(gb, w - vec3(1.0, 0.0, 0.0));
    float vc = dot(gc, w - vec3(0.0, 1.0, 0.0));
    float vd = dot(gd, w - vec3(1.0, 1.0, 0.0));
    float ve = dot(ge, w - vec3(0.0, 0.0, 1.0));
    float vf = dot(gf, w - vec3(1.0, 0.0, 1.0));
    float vg = dot(gg, w - vec3(0.0, 1.0, 1.0));
    float vh = dot(gh, w - vec3(1.0, 1.0, 1.0));
	
    // interpolation
    return va + 
           u.x * (vb - va) + 
           u.y * (vc - va) + 
           u.z * (ve - va) + 
           u.x * u.y * (va - vb - vc + vd) + 
           u.y * u.z * (va - vc - ve + vg) + 
           u.z * u.x * (va - vb - ve + vf) + 
           u.x * u.y * u.z * (-va + vb + vc - vd + ve - vf - vg + vh);
}

// Calculates Worley noise (inverted)
float worleyNoise(vec3 coord, float frequency)
{
    // tile the space
    vec3 id = floor(coord);
    vec3 point = fract(coord);

    // set initial distance value
    float minDist = 10000.0;

    // iterate through the neighbour tiles
    for (float x = -1.0; x <= 1.0; ++x)
    {
        for(float y = -1.0; y <= 1.0; ++y)
        {
            for(float z = -1.0; z <= 1.0; ++z)
            {
                // neighbour place in the grid
                vec3 neighbour = vec3(x, y, z);
                // create a random Worley point for this tile
            	vec3 randomPoint = hash33(mod(id + neighbour, vec3(frequency))) * 0.5 + 0.5;
    			randomPoint += neighbour;

                // distance to the calculated random point
            	vec3 diff = point - randomPoint;
                // keep the closer distance
           		minDist = min(minDist, dot(diff, diff));
            }
        }
    }
    
    // inverted worley noise
    return 1.0f - minDist;
}

// Calculates fBm for the noise defined with fbm at coord
// Expected values for noiseID: {
//      0: Perlin,
//      1: Worley
// }
float noiseFBM(vec3 coord, fbm fbm, int noiseID) {
    // initial values
    float frequency = fbm.frequency;
    float amplitude = fbm.amplitude;
    float noise = 0.0f;
    // loop through octaves
    for (int i = 0; i < fbm.octaves; ++i) {
        // initial value for this noise
        float noiseRes = 0.0f;
        // check which noise to calculate
        if (noiseID == 0) {
            noiseRes = perlinNoise(coord * frequency, frequency);
        } else if (noiseID == 1) {
            noiseRes = worleyNoise(coord * frequency, frequency);
        }
        // accumulate noise
        noise += amplitude * noiseRes;
        // update properties
        frequency *= fbm.lacunarity;
        amplitude *= fbm.gain;
    }
    // return final fBm noise
    return noise;
}

//===============================================================================================
// MAIN
//===============================================================================================

void main()
{
    // get current workgroup pixel
    ivec3 pixel = ivec3(gl_GlobalInvocationID.xyz);
    // calculate current coord
    vec3 coord = vec3(float(pixel.x) / resolution, float(pixel.y) / resolution, float(pixel.z) / resolution);

    // initialize result color
    vec4 col = vec4(0.0);

    // prepare perlin noise fbm
    fbm perlin = fbm(perlinAmplitude, perlinFrequency, perlinOctaves, perlinLacunarity, perlinGain);
    // calculate perlin fBm
    float perlinFBM = noiseFBM(coord, perlin, perlinID);
    
    // prepare worley noise fbm
    fbm worley = fbm(worleyAmplitude, worleyFrequency, worleyOctaves, worleyLacunarity, worleyGain);

    // calculate Worley fBm noises with increasing frequencies
    col.g += noiseFBM(coord, worley, worleyID);
    worley.frequency *= 2.0f;
    col.b += noiseFBM(coord, worley, worleyID);
    worley.frequency *= 2.0f;
    col.a += noiseFBM(coord, worley, worleyID);

    // dilate Perlin fBm with first Worley (Perlin-Worley)
    col.r += remap(perlinFBM, 0.0, 1.0, col.g, 1.0);

    // save final 3D texture
	imageStore(perlinWorleyTex, pixel, col);
}